' {$STAMP BS2}
' {$PBASIC 2.5}

DEBUG "Program Running!"

' ----[ Variables ]--------------------------------------------

pulseCount VAR Word
address VAR Byte
instruction VAR Byte

' ----[ EEPROM Data ]------------------------------------------

'    Address: 0123456789
'             ||||||||||
DATA         "FLFFRBLBBQ"

'----[ Initialization ]----------------------------------------

FREQOUT 4, 2000, 3000

'----[ Main Routine ]------------------------------------------

DO UNTIL (instruction = "Q" )

  READ address, instruction
  address = address + 1

  SELECT instruction
    CASE "F": GOSUB Forward
    CASE "B": GOSUB Backward
    CASE "L": GOSUB Left_Turn
    CASE "R": GOSUB Right_Turn
  ENDSELECT

LOOP

END

'----[ Subroutine - Forward ]-----------------------------

Forward:
  FOR pulseCount = 1 TO 64
    PULSOUT 13, 825
    PULSOUT 12, 650
    PAUSE 20
  NEXT
  RETURN

'----[ Subroutine - Backward ]-----------------------------

Backward:
  FOR pulseCount = 1 TO 64
    PULSOUT 13, 675
    PULSOUT 12, 850
    PAUSE 20
  NEXT
  RETURN

'----[ Subroutine - Left_Turn ]-----------------------------

Left_Turn:
  FOR pulseCount = 1 TO 20
    PULSOUT 13, 650
    PULSOUT 12, 650
    PAUSE 20
  NEXT
  RETURN

'----[ Subroutine - Right_Turn ]-----------------------------

Right_Turn:
  FOR pulseCount = 1 TO 20
    PULSOUT 13, 850
    PULSOUT 12, 850
    PAUSE 20
  NEXT
  RETURN