' Robotics with the Boe-Bot - FollowingBoeBot.bs2
' Boe-Bot adjusts its position to keep objects it detects in zone 2.

' {$STAMP BS2}
' {$PBASIC 2.5}

DEBUG "Program running!"

' CONSTANTS

kpl              CON    -35
kpr              CON    35
SetPoint         CON    2
CenterPulse      CON    750

' VARIABLES

freqSelect       VAR    Nib
irFrequency      VAR    Word
irDetectLeft     VAR    Bit
irDetectRight    VAR    Bit
distanceLeft     VAR    Nib
distanceRight    VAR    Nib
pulseLeft        VAR    Word
pulseRight       VAR    Word

' INITIALIZATION

FREQOUT 4, 2000, 3000

' MAIN ROUTINE

DO
  GOSUB get_ir_distances

  pulseLeft = (SetPoint - distanceLeft)*kpl + CenterPulse
  pulseRight = (SetPoint - distanceRight)*kpr + CenterPulse

  GOSUB send_pulse
LOOP

' SUBROUTINE

get_ir_distances:
  distanceLeft = 0
  distanceRight = 0
  FOR freqSelect = 0 TO 4
    LOOKUP freqSelect, [37500, 38250, 39500, 40500, 41500], irFrequency

    FREQOUT 8, 1, irFrequency
    irDetectLeft = IN9
    distanceLeft = distanceLeft + irDetectLeft

    FREQOUT 2, 1, irFrequency
    irDetectRight = IN0
    distanceRight = distanceRight + irDetectRight
  NEXT
  RETURN

send_pulse:
  PULSOUT 13, pulseLeft
  PULSOUT 12, pulseRight
  PAUSE 5
  RETURN
