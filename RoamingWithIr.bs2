' Robotics with the Boe-Bot - RoamingWithIr.bs2
' Adapt RoamingWithWhiskers.bs2 for use with IR object detectors.
' {$STAMP BS2}
' {$PBASIC 2.5}

DEBUG "Program running!"

' VARIABLES

irDetectLeft   VAR              Bit
irDetectRight  VAR              Bit
pulseCount     VAR              Byte

' INITIALIZATION

FREQOUT 4, 2000, 3000

' MAIN ROUTINE

DO
  FREQOUT 8, 1, 38500
  irDetectLeft = IN9

  FREQOUT 2, 1, 38500
  irDetectRight = IN0

  IF (irDetectLeft = 0) AND (irDetectRight = 0) THEN
    GOSUB back_up
    GOSUB turn_left
    GOSUB turn_left
  ELSEIF (irDetectLeft = 0) THEN
    GOSUB back_up
    GOSUB turn_right
  ELSEIF (irDetectRight = 0) THEN
    GOSUB back_up
    GOSUB turn_left
  ELSE
    GOSUB forward_pulse
  ENDIF
LOOP

forward_pulse:
  PULSOUT 13, 825
  PULSOUT 12, 650
  PAUSE 20
  RETURN

turn_left:
  FOR pulseCount = 0 TO 20
    PULSOUT 13, 650
    PULSOUT 12, 650
    PAUSE 20
  NEXT
  RETURN

turn_right:
  FOR pulseCount = 0 TO 20
    PULSOUT 13, 850
    PULSOUT 12, 850
    PAUSE 20
  NEXT
  RETURN

back_up:
  FOR pulseCount = 0 TO 40
    PULSOUT 13, 675
    PULSOUT 12, 850
    PAUSE 20
  NEXT
  RETURN